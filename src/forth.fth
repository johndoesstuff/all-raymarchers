\ INTENDED FOR EVALUATION ON GFORTH
\
\ IM SURE A LOT OF THIS IS BAD PRACTICE BUT
\ THIS LANGUAGE WAS A CHALLENGE AND THIS IS
\ MY FIRST FORTH PROGRAM!

VARIABLE SCREEN-WIDTH 	50 SCREEN-WIDTH !
VARIABLE SCREEN-HEIGHT 	20 SCREEN-HEIGHT !
VARIABLE STEPS 		40 STEPS !

FVARIABLE SPACE-REPEAT 	5.0E SPACE-REPEAT F!
FVARIABLE SPHERE-RADIUS 1.5E SPHERE-RADIUS F!

CREATE CAMERA 	1.0E F, 2.0E F, -4.0E F,
CREATE POSITION	0.0E F, 0.0E F, 0.0E F,

\ MAKE INDEXABLE
: ]CAMERA	{ N -- VAL } N FLOATS CAMERA + ;
: ]POSITION	{ N -- VAL } N FLOATS POSITION + ;

FVARIABLE TRAVELED	0.0E TRAVELED F!

: FSQUARED { F: FX -- FX**2 } FX 2.0E F** ;

: FMOD+ { F: X F: N } X N X N F/ FLOOR F* F- ;
: FWRAP { F: X F: S } S 0.5E F* X F+ S FMOD+ S 0.5E F* F- ;
: PWRAP { F: X F: Y F: Z }
	X SPACE-REPEAT F@ FWRAP
	Y SPACE-REPEAT F@ FWRAP
	Z SPACE-REPEAT F@ FWRAP
	;

: DIST { F: FX F: FY F: FZ -- FD }
	FX FSQUARED
	FY FSQUARED
	FZ FSQUARED
	F+ F+
	FSQRT
	;

: SDF { F: FX F: FY F: FZ -- SDF }
	FX FY FZ DIST
	SPHERE-RADIUS F@
	F-
	;

: NORMALIZE { F: FX F: FY F: FZ -- FNX FNY FNZ }
	FX FY FZ DIST
	FDUP FX FSWAP F/ FSWAP
	FDUP FY FSWAP F/ FSWAP
	FZ FSWAP F/
	;

: P* { F: FX F: FY F: FZ F: C -- }
	FX C F*
	FY C F*
	FZ C F*
	;

: P+ { F: AX F: AY F: AZ F: BX F: BY F: BZ }
	AX BX F+
	AY BY F+
	AZ BZ F+
	;

: GET-CAMERA 0 ]CAMERA F@ 1 ]CAMERA F@ 2 ]CAMERA F@ ;

: GET-POSITION 0 ]POSITION F@ 1 ]POSITION F@ 2 ]POSITION F@ ;
: SET-POSITION 2 ]POSITION F! 1 ]POSITION F! 0 ]POSITION F! ;

: ACCUM-TRAVELED { F: SAFE } TRAVELED F@ SAFE F+ TRAVELED F! SAFE ;

: MARCH-RAY { F: DIRX F: DIRY F: DIRZ }
	DIRX DIRY DIRZ GET-POSITION SDF
	ACCUM-TRAVELED
	P*
	GET-POSITION P+
	PWRAP
	SET-POSITION
	DIRX DIRY DIRZ
	;

: ?DRAW
	TRAVELED F@ 5.0E F< IF 35 EMIT ELSE
	TRAVELED F@ 10.0E F< IF 124 EMIT ELSE
	TRAVELED F@ 15.0E F< IF 58 EMIT ELSE
	TRAVELED F@ 20.0E F< IF 46 EMIT ELSE
	32 EMIT THEN THEN THEN THEN
	;

: PIXEL>RAYDIR { X Y }
	X S>F SCREEN-WIDTH @ S>F F/ 0.5E F-
	Y S>F SCREEN-HEIGHT @ S>F F/ 0.5E F-
	1.0E
	NORMALIZE
	;
	
: DO-PIXEL { X Y }
	X Y
	PIXEL>RAYDIR
	GET-CAMERA
	SET-POSITION
	0.0E TRAVELED F!
	GET-POSITION
	SET-POSITION
	STEPS @ 1 DO
		MARCH-RAY
	LOOP
	GET-POSITION
	SET-POSITION
	?DRAW
	FDROP FDROP FDROP \ CLEAR DIR
	;

: MAIN-RENDER
	SCREEN-HEIGHT @ 0 DO
		SCREEN-WIDTH @ 0 DO
			I J DO-PIXEL
		LOOP
		CR
	LOOP
	;

\ RUN RAYMARCHER
MAIN-RENDER
